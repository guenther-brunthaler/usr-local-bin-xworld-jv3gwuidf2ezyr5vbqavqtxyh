#! /bin/sh

# Filter dmsetup table contents read from standard input by replacing all
# hexadecimal key fields with "?"-strings of the same length. This allows to
# display the contents of those tables without also displaying the keys.
#
# v2023.279.2

set -e
trap 'test $? = 0 || echo "\"$0\" failed!" >& 2' 0

rchar='?'
while getopts z opt
do
	case $opt in
		z) rchar=0;;
		*) false || exit
	esac
done
shift `expr $OPTIND - 1 || :`

# Check whether awk is modern enough and supports named RegEx classes.
echo 9aA | awk '!/^[[:xdigit:]]+$/ {exit system("false")}'

awk -v rchar="$rchar" -f /dev/fd/5 5<< 'EO_AWK' -- ${1+"$@"}

BEGIN {
	if (annotate = ARGC > 1) {
		if (system("test -t 1")) annotate = 0
	}
}

annotate && FNR == 1 { print "# File \"" FILENAME "\":" >> "/dev/stderr" }

$3 == "crypt" && $5 ~ /^[[:xdigit:]]+$/ {
	o = ""
	for (i = length($5); i--; ) o = o rchar
	$5 = o
}

{print}

EO_AWK
